// Full user guide available at:
// https://docs.gradle.org/2.10/userguide/tutorial_java_projects.html

apply plugin: 'java'
apply plugin: 'application' // able to create executables
                            // automatically includes the 'distribution' plugin
apply plugin: 'idea'        // able to create IntelliJ project files
                            // not required for modern IntelliJ
apply plugin: 'eclipse'     // able to create Eclipse project files

/*

    run a task by running:
        gradle TASK_NAME

    or using the wrapper:
        gradlew TASK_NAME

    to run the default tasks, run gradle with no arguments


Useful gradle tasks:

      task    | plugin       |     description
    ----------|--------------|------------------------------------------------------
      tasks   |              | list available tasks (including custom ones)
              |              |
      build   | java         | compiles and checks (tests) the application
      check   | java         | runs all verification (including the 'test' task)
      clean   | java         | removes the build directory (safe to do)
              |              |
      run     | application  | sets the classpath and runs the application
      distZip | application  | bundles dependencies and installer into a zip file


Project File tasks:

    For modern IntelliJ, import (not open) the gradle project as is
    (it understands gradle)

      task         | plugin  |  description
    ---------------|---------|------------------------------------------------------
      eclipse      | eclipse |  creates Eclipse project files
      cleanEclipse | eclipse |  removes Eclipse project files


Custom tasks:

    - deploymentJar     a jar file bundled with all required dependencies

*/




sourceCompatibility = 1.8   // JVM version to use when compiling
targetCompatibility = 1.8   // JVM version to build for

def name = 'JavaFX Prototype'
version  = '0.01'

mainClassName = 'javafxprototype.Main'

defaultTasks 'build', 'run'



repositories {
    // not used
    // jcenter()

    // search for packages here: https://search.maven.org
    mavenCentral()
}


dependencies {
    // JFExtras
        compile 'org.jfxtras:jfxtras-controls:8.0-r4'
        compile 'org.jfxtras:jfxtras-labs:8.0-r4'    // for experimental features

    // Rich Text FX
        compile group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.6.10'

    // Testing
        testCompile 'junit:junit:4.12'
}


jar {
    manifest {
        attributes 'Implementation-Title':   name,
                   'Implementation-Version': version,
                   'Main-Class':             mainClassName
    }
}


// bundle a jar with all dependencies
// modified from:
// http://www.mkyong.com/gradle/gradle-create-a-jar-file-with-dependencies/
task deploymentJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title':   name,
                   'Implementation-Version': version,
                   'Main-Class':             mainClassName
    }
    baseName = project.name + '-bundled-depends'

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

