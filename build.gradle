// Full user guide available at:
// https://docs.gradle.org/2.10/userguide/tutorial_java_projects.html
/* Groovy notes:
- double quotes are necessary to embed variables using "$myvar" or "${myvar.thing}"
- task { stuff } is for configuration (run every time any task is run)
- task << { stuff } is to define the action to be taken when a task is run
 */


/* Task Overview:

    run a task by running:
        gradle TASK_NAME

    or using the wrapper:
        gradlew TASK_NAME

    to run the default tasks, run gradle with no arguments


Useful gradle tasks:

      task    | plugin       |     description
    ----------|--------------|------------------------------------------------------
      tasks   |              | list available tasks (including custom ones)
              |              |
      build   | java         | compiles and checks (tests) the application
      check   | java         | runs all verification (including the 'test' task)
      clean   | java         | removes the build directory (safe to do)
              |              |
      run     | application  | sets the classpath and runs the application
      distZip | application  | bundles dependencies and installer into a zip file


Project File tasks:

    For modern IntelliJ, import (not open) the gradle project as is
    (it understands gradle)

    For eclipse, use the 'Buildship' plugin
    http://projects.eclipse.org/projects/tools.buildship

    File > Import > Gradle Project


Custom tasks:

    - deploymentJar     a jar file bundled with all required dependencies

*/

// from https://plugins.gradle.org/
plugins {
    id 'org.ajoberstar.grgit' version '1.4.2'   // git dependencies
    id "de.undercouch.download" version "2.1.0" // HTTP download dependencies

    // able to compile java files
    id 'java'

    // able to create parsers from Antlr grammars.
    // task compileJava automatically depends on generateGrammarSource
    id 'antlr'

    // able to create executables. Automatically includes the 'distribution' plugin
    id 'application'

    // able to create IntelliJ project files (not required for modern IntelliJ)
    // to sync with gradle: gradle toolbar (default on the right) and press the
    // refresh icon
    id 'idea'

    // able to create Eclipse project files
    //
    // to sync with gradle: close eclipse. Run `gradle cleanEclipse eclipse` which
    // will regenerate the project files, re-open eclipse.
    //
    // better option: use the Eclipse plugin: 'Buildship'
    // http://projects.eclipse.org/projects/tools.buildship
    id 'eclipse'
}

/////////////////////////////////////////////////
//
// Gradle Scripts
//
/////////////////////////////////////////////////

apply from: 'gradle/antlr.gradle'
apply from: 'gradle/ace.gradle'
apply from: 'gradle/build-metadata.gradle'
apply from: 'gradle/dependencies.gradle'
apply from: 'gradle/testing.gradle'
apply from: 'gradle/deployment.gradle'

/////////////////////////////////////////////////
//
// Project Properties
//
/////////////////////////////////////////////////


def name = rootProject.name // defined in settings.gradle
version = '0.3'

description = 'MIPS-like assembly language interpreter' +
        ' with CPU and high-level algorithm visualisations'

mainClassName = 'simulizer.Simulizer'

run.workingDir = file('work')


/////////////////////////////////////////////////
//
// Build Properties
//
/////////////////////////////////////////////////

compileJava {
    sourceCompatibility = 1.8   // JVM version to use when compiling
    targetCompatibility = 1.8   // JVM version to build for
    options.compilerArgs << '-Xlint:all' << '-Werror'
}

defaultTasks 'build', 'run'
